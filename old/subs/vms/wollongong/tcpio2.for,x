c************************************************************************
	subroutine tcpnode(remote,inet)
c
	implicit none
	character remote*(*)
	byte inet(4)
c
c  Determine the inet address of a host. If the host is not found, zero is
c  returned.
c
c  VAX version. Host table has the Wollongong name.
c
c  Input:
c    remote	The name of the host to look up.
c  Output:
c    inet	The internet address. This normally will be passed in as
c		an integer, but we declare it as byte here for convenience.
c
c  History:
c    rjs  24aug89 Created from a subroutine in rcp.
c
c------------------------------------------------------------------------
	integer lu,status,length,k1,k2,lrem,i,l0
	logical more,ok
	character line*140,num*3
	double precision d
c
c  Externals.
c
	integer len1
c
c  Check for pathological cases.
c
	lrem = len1(remote)
	if(lrem.le.0)
     *	  call bug('f','Host name is zero length, in TCPNODE')
	if(lrem+8.gt.len(line))
     *	  call bug('f','Host name is too long, in TCPNODE')
c
c  Check if the name is in the dot notation.
c
	if(remote(1:1).ge.'0'.and.remote(1:1).le.'9')then
	  status = 0
	  length = lrem
	  line(1:length) = remote(1:lrem)
c
c  "Remote" is a name. Search through the hosts file for it. Make sure
c  we are not fooled by things that look nearly right.
c
	else
	  more = .true.
	  call txtopen(lu,'twg$etc:[000000]hosts','old',status)
	  if(status.eq.0)call txtread(lu,line,length,status)
	  do while(status.eq.0.and.more)
	    if(length.gt.len(line))then
	      call bug('w','Line in etc:hosts too long -- ignored')
	    else if(length.gt.lrem+8	.and.
     *		  line(1:1).ne.'#')	then
	      call lcase(line(1:length))
	      l0 = 0
	      dowhile(l0+lrem.le.length.and.more)
	        i = index(line(l0+1:length),remote(1:lrem)) + l0
	        if(i.eq.l0)i = length + 1
	        if(i.gt.1.and.i+lrem-1.le.length)then
	          if(line(i-1:i-1).le.' ')then
	 	    if(i+lrem-1.eq.length)then
		      more = .false.
		    else if(line(i+lrem:i+lrem).le.' ')then
		      more = .false.
		    endif
	          endif
	 	  l0 = i
	        else
		  l0 = length
	        endif
	      enddo
	    endif
	    if(more)call txtread(lu,line,length,status)
	  enddo
	  call txtclose(lu)
	endif
c
c  We have a internet address in dot notation. Now decode it into four integers.
c  Note the fiddle to copy with signed bytes.
c
	if(status.ne.0)then
	  do i=1,4
	    inet(i) = 0
	  enddo
	else
	  k1 = 1
	  k2 = length
	  do i=1,4
	    call gettok(line,k1,k2,num,length)
	    if(length.gt.0.and.length.le.len(num))then
	      call atod(num(1:length),d,ok)
	    else
	      ok = .false.
	    endif
	    if(.not.ok)call bug('f','Error decoding inet address')
	    if(d.gt.127)d = d - 256
	    inet(i) = nint(d)
	    k1 = k1 + 1
	  enddo
	endif
c
	end
