#!/bin/csh -f
#
# Compile and link a module, and place it in the directory of executables.
#

if ( -e $MIRLIB/libmir.a ) then
  if ( "`mirnewer -i $MIRLIB/Last_ranlib $MIRLIB/libmir.a`" != "" ) then
    ranlib $MIRLIB/libmir.a
    echo "`date` `hostname` `whoami`" >> $MIRLIB/Last_ranlib
  endif
endif

foreach name ($*)
  set file = $name:t
  set file = $file:r
  set tmp=/tmp/tmp$$
  set doc = $file.doc
  set dir = $name:h
  if ( $dir == $name ) set dir = "."
  echo Processing $name
  switch($name)
    case *.for
      (cd $dir; ratty -s convex -I $MIRINC $name $tmp.f)
      fc -or none -O2 -o $MIRBIN/$file $tmp.f `mirlibs`
      rm -f $tmp.f
      rm -f $doc
      doc -p $name
      if ( -e $doc ) then
        echo Updating $doc
        mv $doc $MIRPDOC
      endif
      breaksw
    case *.csh:
      rm -f $MIRBIN/$file
      cp $name $MIRBIN/$file
      chmod +x $MIRBIN/$file
      breaksw
    case *.c:
      (cd $dir; cc -O -Dconvex -I$MIRINC -o $MIRBIN/$file $name)
      breaksw
    case *.doc:
      cp $name $MIRPDOC
      breaksw
    default:
      echo Ignoring $name
      breaksw
  endsw
end
