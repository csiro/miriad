#! /bin/csh -ef


# Run ranlib if needed.

if ( -e $MIRLIB/libmir.a ) then
  if ( "`mirnewer -i $MIRLIB/Last_ranlib $MIRLIB/libmir.a`" != "" ) then
    echo "Running ranlib/building sharable library ..."
    ranlib $MIRLIB/libmir.a
    ld -shared -o $MIRLIB/libmir.so --whole-archive $MIRLIB/libmir.a
    echo Last ranlib at `date` by `whoami` >> $MIRLIB/Last_ranlib
    touch $MIRLIB/Last_ranlib
  endif
endif

foreach name ($*)
  set file = $name:t
  set file = $file:r
  set tmp=/tmp/tmp$$
  set doc = $file.doc
  set dir = $name:h
  if ( $dir == $name ) set dir = "."
  echo Processing $name
  switch($name)
    case *.for
      (cd $dir; $MIRBIN/ratty -s linux -b -I $MIRINC $name $tmp.f)
#      f77 -ff90-intrinsics-hide -O -s -o $MIRBIN/$file $tmp.f `$MIRBIN/mirlibs`
      f77 -O -o $MIRBIN/$file $tmp.f `$MIRBIN/mirlibs`
      chmod 775 $MIRBIN/$file
      rm -f $tmp.f
      rm -f $doc
      $MIRBIN/doc -p $name
      if ( -e $doc ) then
        echo Updating $doc
        mv -f $doc $MIRPDOC
      endif
      breaksw
    case *.c:
      (cd $dir; rm -f $MIRBIN/$file ; cc -O -Dlinux -I$MIRINC -s -o $MIRBIN/$file $name)
      chmod 775 $MIRBIN/$file
      breaksw
    case *.csh:
      rm -f $MIRBIN/$file
      cp $name $MIRBIN/$file
      chmod 775 $MIRBIN/$file
      breaksw
    case *.doc:
      if ( -e $MIRPDOC/$name ) then
        rm -f $MIRPDOC/$name
      endif
      cp $name $MIRPDOC
      breaksw
    default:
      echo Ignoring $name
      breaksw
  endsw
end


