#! /bin/csh -ef
#-----------------------------------------------------------------------------
# $Id: mirclnk.csh,x 0.2 2007/10/30 08:24:01 cal103 Exp $
#-----------------------------------------------------------------------------

foreach name ($*)
  set file = $name:t
  set file = $file:r
  set tmp=/tmp/tmp$$
  set doc = $file.doc
  set dir = $name:h
  if ( $dir == $name ) set dir = "."
  echo Processing $name

  switch($name)
    case *.for
      (cd $dir; $MIRBIN/ratty -s sun -b -I $MIRINC $name $tmp.f)
      rm -f $MIRBIN/$file
      f77 -O2 -libmil -o $MIRBIN/$file $tmp.f `$MIRBIN/mirlibs`
      chmod 775 $MIRBIN/$file
      rm -f $tmp.f
      rm -f $doc
      $MIRBIN/doc -p $name
      if ( -e $doc ) then
        echo Updating $doc
        mv -f $doc $MIRPDOC
        chmod 664 $MIRPDOC/$doc
      endif
      breaksw

    case *.c:
      rm -f $MIRBIN/$file
      (cd $dir; cc -O -Dsun -I$MIRINC -o $MIRBIN/$file $name)
      chmod 775 $MIRBIN/$file
      breaksw

    case *.csh:
      rm -f $MIRBIN/$file
      cp $name $MIRBIN/$file
      chmod 775 $MIRBIN/$file
      breaksw

    case *.doc:
      rm -f $MIRPDOC/$doc
      cp $name $MIRPDOC
      chmod 664 $MIRPDOC/$doc
      breaksw

    default:
      echo Ignoring $name
      breaksw
  endsw
end
