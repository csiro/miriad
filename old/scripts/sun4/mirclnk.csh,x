#! /bin/csh -f

if ( -e $MIRLIB/libmir.a ) then
  if ( "`mirnewer -i $MIRLIB/Last_ranlib $MIRLIB/libmir.a`" != "" ) then
    echo "Running ranlib ..."
    ranlib $MIRLIB/libmir.a
    echo "`date` `hostname` `whoami`" >> $MIRLIB/Last_ranlib
  endif
endif

foreach name ($*)
  set file = $name:t
  set file = $file:r
  set tmp=/tmp/tmp$$
  set doc = $file.doc
  set dir = $name:h
  if ( $dir == $name ) set dir = "."
  echo Processing $name
  switch($name)
    case *.for
      (cd $dir; ratty -s sun -b -I $MIRINC $name $tmp.f)
      f77 -temp=$MIR -O2 -libmil -o $MIRBIN/$file $tmp.f `mirlibs`
      rm -f $tmp.f
      rm -f $doc
      doc -p $name
      if ( -e $doc ) then
        echo Updating $doc
        mv $doc $MIRPDOC
      endif
      breaksw
    case *.c:
      (cd $dir;cc -temp=$MIR -O -Dsun -I$MIRINC -o $MIRBIN/$file $name)
      breaksw
    case *.csh:
      rm -f $MIRBIN/$file
      cp $name $MIRBIN/$file
      chmod +x $MIRBIN/$file
      breaksw
    case *.doc:
      cp $name $MIRPDOC
      breaksw
    default:
      echo Ignoring $name
      breaksw
  endsw
end
