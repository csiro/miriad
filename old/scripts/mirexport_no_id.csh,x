#!/usr/bin/csh -f

#  Generate a tar file containing files that have changed
#  for a system without an establised ID.
#
#  Call as mirexport_new_system ID Incremental_file
#
#  Where:
#
#  Incremental_file - the first Incremental update file
#        since the user's system was last updated. No path required.
#
#  ID - new ID to use for the system.
#
#  DPR 22-05-01
#

set exportdir = $MIR/ftp/updates
set diffdir   = $MIR/new
set olddir    = $MIR/old

set rhost = ${1}
set oldfile = ${2}


# Clean out the export directory of anything that has already been
# received.

touch  $exportdir/junk.Done
foreach file ( $exportdir/*.Done )
  rm -rf $file $file:r
end

# Generate the list of the files to update.


cd $MIR
if ( -e ${olddir}/${oldfile} ) then
  set cond = (-newer ${olddir}/${oldfile})
else
  echo "ERROR: unable to locate file ${olddir}/${oldfile}"
  exit
endif

find cat inc linpack prog scripts spec specdoc subs tools \
 ! -type d \! -name junk\* \! -name \*.o \! -name rpfits.inc \
 ! -name \*.log \! -name \*.dvi \! -name \*.def $cond \
 -print > /tmp/${rhost}_files.$$
mirnewer -i ${olddir}/${oldfile} ./README      >> /tmp/${rhost}_files.$$
mirnewer -i ${olddir}/${oldfile} ./Install     >> /tmp/${rhost}_files.$$
mirnewer -i ${olddir}/${oldfile} ./DISCLAIMER  >> /tmp/${rhost}_files.$$
mirnewer -i ${olddir}/${oldfile} ./changes.txt >> /tmp/${rhost}_files.$$

if ( -z /tmp/${rhost}_files.$$ ) exit

echo There are some files to process

# Determine the filename of the output tar file

set date = `date +%Y_%m_%d-%H%M`

if ( ! -d $MIR/export ) mkdir $MIR/export
set file = $exportdir/${rhost}_${date}.tar

# Create the output tar file.

tar -cf $file `cat /tmp/${rhost}_files.$$`
if ( $status ) exit
compress -f $file
if ( $status ) exit

# Remember the time that we did this.
touch $MIR/export/Last_update_${rhost}
echo Tar file written at `date` by `whoami` >> $MIR/export/Last_update_${rhost}

# Send a message to let me know what was copied to the tar file.

echo Generated tar file $file, containing
cat /tmp/${rhost}_files.$$
rm /tmp/${rhost}_files.$$
