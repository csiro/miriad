$ verf = 'f$verify("NO")'
$! This is a command procedure to perform selective updates of particular
$! files. No check is made that they are in the correct hierarchy, nor
$! of their modification times. 
$
$! Inputs:
$!  p1		Name of machine to update.
$!  p2		List of files to update.
$
$! Generate the list of files to update.
$
$	on error then goto 90
$	on control_y then goto 90
$	status = 1
$	directory/versions=1/noheader/notrailer/output=junk.tmp1 'p2'
$	sort junk.tmp1 junk.tmp2
$	delete junk.tmp1;
$
$! Generate the file used to determine where the output file should be stored.
$
$	open/read hierarchy mirupd:hierarchy.'p1'
$	open/write translate junk.tmp1
$20:	n = 0
$	read/end=40 hierarchy line
$	output = f$element(0," ",line)
$	path = f$element(1," ",line)
$25:	if f$extract(f$length(path)-1,1,path).nes."-" then goto 30
$	read hierarchy line
$	path = f$edit(f$extract(0,f$length(path)-1,path)+line,"collapse")
$30:	leaf = f$element(n,",",path)
$	if leaf.eqs."," then goto 20
$	n = n + 1
$	write translate f$edit(leaf,"upcase")," ",output
$	goto 30
$40:	close hierarchy
$	close translate
$	sort junk.tmp1 junk.tmp3
$
$! Go through the two lists, checking where to put the output.
$
$	open/read translate junk.tmp3
$	open/read file junk.tmp2
$	open/write process junk.tmp1
$	leaf = ""
$50:	read/end=75 file file
$	dir = f$parse(file,,,"directory")
$60:	if leaf.eqs.dir then goto 70
$	read/end=61 translate line
$	goto 62
$61:	write sys$output "%ONE_OFF-F-NOT_FOUND, did not find path for ",dir
$	goto 90
$62:	leaf = f$element(0," ",line)
$	output = f$element(1," ",line)
$	goto 60
$70:	out = output + f$parse(file,,,"name") + f$parse(file,,,"type")
$	out = f$edit(out,"lowercase")
$	write process file," ",out
$	goto 50
$75:	close translate
$	close file
$	close process
$
$! Do a once-off update.
$
$	@mirupd:update_com.'p1' junk.tmp1 'p1'
$	status = '$status'
$	if .not.status then goto 90
$
$! Keep track of when and what things were updated.
$
$	if f$search("mirupd:updates.dat").eqs."" then create mirupd:updates.dat
$	on error then goto 90
$	open/append file mirupd:updates.dat
$	open/read process junk.tmp1
$	write file "*** One-off updates to ",p1," at ",f$time()
$80:	read/end=90 process line
$	write file line
$	goto 80
$
$90:	if f$logical("process").nes."" then close process
$	if f$logical("file").nes."" then close file
$	if f$logical("translate").nes."" then close translate
$	if f$logical("hierarchy").nes."" then close hierarchy
$	if .not.status then write sys$output f$message(status)
$	if verf then set verify
