c************************************************************************
	program velsw
	implicit none
c= velsw -- Change the `velocity' axis of an image.
c& rjs
c: map manipulation
c+
c	VELSW is a Miriad task which changes the units on the `velocity'
c	axis. As velocity and frequency are related by a Doppler shift
c	formula, it is possible to switch an axis between being labelled
c	in frequency or velocity. VELSW can switch the axis description
c	between frequency (labelled FREQ-) and velocity, using either
c	the `radio convention' (labelled VELO-) or `optical convention'
c	(labelled FELO-) for the formula relating velocity 
c	and frequency.
c
c	Assuming that the data were measured with equal frequency
c	increments, some approximation is involved assigning a velocity
c	axis increment for the optical convention. In this case,
c	the increment stored is correct at the reference pixel.
c@ in
c	The name of the input image data set. No default.
c@ axis
c	This determines what the labelling on the `velocity' axis will
c	be changed to. Possible values are "frequency", "optical" (for
c	velocity using the optical convention) or "radio" (for velocity
c	using the radio convention). The default is "frequency" if the
c	`velocity' axis is given as a velocity, and "radio" otherwise.
c--
c  History:
c    rjs  31aug93 Original version.
c    nebk 07oct93 Doc change
c  Bugs:
c------------------------------------------------------------------------
	include 'mirconst.h'
	double precision c
	parameter(c=0.001*cmks)
	include 'maxdim.h'
	include 'maxnax.h'
	character version*(*)
	parameter(version='VelSw: version 1.0 31-Aug-93')
c
	character in*64,aname*1,num*2,ctype*16
	integer nr,nout,nsize(MAXNAX),lIn
	double precision crval,cdelt,f,df,restfreq,vobs
c
	integer nswitch
	parameter(nswitch=3)
	character switches(nswitch)*9,switch*9,insw*9,frame*3
c
c  Externals.
c
	character itoaf*2
c
	data switches/'frequency','optical  ','radio    '/
c
c  Get the input parameters.
c
	call output(version)
	call keyini
	call keya('in',in,' ')
	call keymatch('axis',nswitch,switches,1,switch,nout)
	call keyfin
c
c  Check the inputs.
c
	if(in.eq.' ')call bug('f','An input must be given')
c
c  Open the input, and find the velocity axis.
c
	call xyopen(lIn,in,'old',MAXNAX,nsize)
	aname = ' '
	nr = 0
	call fndaxnum(lIn,'freq',aname,nr)
	if(nr.eq.0)call bug('f','Could not find velocity axis')
	num = itoaf(nr)
c
c  Get the values that we need.
c
	call rdhda(lIn,'ctype'//num,ctype,' ')
	call rdhdd(lIn,'cdelt'//num,cdelt,0.d0)
	call rdhdd(lIn,'crval'//num,crval,0.d0)
	call rdhdd(lIn,'restfreq',restfreq,0.d0)
	call rdhdd(lIn,'vobs',vobs,0.d0)
c
c  Determine what ctype means.
c
	if(ctype(1:4).eq.'FELO')then
	  insw = 'optical'
	else if(ctype(1:4).eq.'VELO')then
	  insw = 'radio'
	else if(ctype(1:4).eq.'FREQ')then
	  insw = 'frequency'
	else
	  call bug('f','Unrecognised value for ctype')
	endif
	if(ctype(5:5).eq.'-')then
	  frame = ctype(6:8)
	else
	  frame = '???'
	endif
c
c  Determine the thing to swithc to, if it was not given by the user.
c
	if(switch.eq.' ')then
	  if(insw.eq.'radio'.or.insw.eq.'optical')then
	    switch = 'frequency'
	  else
	    switch = 'radio'
	  endif
	endif
c
c  Determine the frequency of the reference channel.
c
	if(insw.eq.'optical')then
	  f = restfreq/(1+(crval+vobs)/c)
	  df = -(cdelt/c) * f * (f/restfreq)
	else if(insw.eq.'radio')then
	  f = restfreq*(1-(crval+vobs)/c)
	  df = -(cdelt/c) * restfreq
	else
	  f = crval
	  df = cdelt
	endif
c
c  Perform the transformation.
c
	if(switch.eq.insw)then
	  call bug('w','No conversion to be performed')
	else if(switch.eq.'optical')then
	  crval =  c*(restfreq/f-1) - vobs
	  cdelt = -c*(df/f)*(restfreq/f)
	  ctype = 'FELO-'//frame
	else if(switch.eq.'radio')then
	  crval =  c*(1-f/restfreq) - vobs
	  cdelt = -c*(df/restfreq)
	  ctype = 'VELO-'//frame
	else if(switch.eq.'frequency')then
	  crval = f
	  cdelt = df
	  ctype = 'FREQ-'//frame
	endif
c
c  Write out the new information.
c
	call wrhda(lIn,'ctype'//num,ctype)
	call wrhdd(lIn,'cdelt'//num,cdelt)
	call wrhdd(lIn,'crval'//num,crval)
c
c  Write out some history.
c
	call hisopen(lIn,'append')
	call hiswrite(lIn,'VELSW: Miriad '//version)
	call hisinput(lIn,'VELSW')
	call hisclose(lIn)
c
c  All said and done. Close up.
c
	call xyclose(lIn)
c	
	end
